PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
PREFIX dbpprop: <http://dbpedia.org/property/>

WITH <http://imn.htwk-leipzig.de/pbachman/ontologies/movie-soundtracks#>
INSERT
{
    GRAPH <http://imn.htwk-leipzig.de/pbachman/ontologies/references#>
    { 
        ?imdbFilm owl:sameAs ?tunefindFilm .
        ?tunefindFilm owl:sameAs ?imdbFilm .
    }
}
WHERE
{
    ?imdbFilm rdf:type dbpedia-owl:Film ;
              rdfs:label ?imdbFilmLabel .

    FILTER(STRSTARTS(STR(?imdbFilm), "http://imn.htwk-leipzig.de/pbachman/ontologies/imdb#"))

    ?tunefindFilm rdf:type dbpedia-owl:Film ;
                  rdfs:label ?tunefindFilmLabel .  

    FILTER(STRSTARTS(STR(?tunefindFilm), "http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#"))


    FILTER(lcase(STR(?tunefindFilmLabel)) = lcase(STR(?imdbFilmLabel)))
}