PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
PREFIX dbpprop: <http://dbpedia.org/property/>

INSERT
{
    GRAPH <http://imn.htwk-leipzig.de/pbachman/ontologies/references#>
    { 
        ?tunefindSong owl:sameAs ?chartSong .
        ?lastfmSong owl:sameAs ?chartSong .
        ?chartSong owl:sameAs ?tunefindSong .
        ?chartSong owl:sameAs ?lastfmSong .

        ?tunefindArtist owl:sameAs ?chartArtist .
        ?lastfmArtist owl:sameAs ?chartArtist .
        ?chartArtist owl:sameAs ?tunefindArtist .
        ?chartArtist owl:sameAs ?lastfmArtist .
    }
}
USING <http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#>
USING <http://imn.htwk-leipzig.de/pbachman/ontologies/references#>
USING <http://imn.htwk-leipzig.de/pbachman/ontologies/charts_de#>
WHERE
{
    ?tunefindSong a dbpedia-owl:Song ;
                  dbpedia-owl:artist ?tunefindArtist ;
                  dbpprop:title ?tunefindSongTitle ;
                  owl:sameAs ?lastfmSong .

    ?tunefindArtist a dbpedia-owl:MusicalArtist ;
                    dbpprop:name ?tunefindArtistName ;
                    owl:sameAs ?lastfmArtist .

    FILTER(STRSTARTS(STR(?tunefindSong), "http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#"))
    FILTER(STRSTARTS(STR(?lastfmSong), "http://imn.htwk-leipzig.de/pbachman/ontologies/lastfm#"))
    FILTER(STRSTARTS(STR(?lastfmArtist), "http://imn.htwk-leipzig.de/pbachman/ontologies/lastfm#"))

    ?chartSong a dbpedia-owl:Song ;
               dbpedia-owl:artist ?chartArtist ;
               dbpprop:title ?chartSongTitle .

    ?chartArtist a dbpedia-owl:MusicalArtist ;
                 dbpprop:name ?chartArtistName .

    FILTER(STRSTARTS(STR(?chartSong), "http://imn.htwk-leipzig.de/pbachman/ontologies/charts_de#"))


    FILTER(
      lcase(
        SUBSTR(STR(?tunefindSongTitle), 1, 7)
      )
      =
      lcase(
        SUBSTR(STR(?chartSongTitle), 1, 7)
      )
    )

    FILTER(
      lcase(
        SUBSTR(STR(?tunefindArtistName), 1, 7)
      )
      =
      lcase(
        SUBSTR(STR(?chartArtistName), 1, 7)
      )
    )
}