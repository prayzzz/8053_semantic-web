PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
PREFIX dbpprop: <http://dbpedia.org/property/>

INSERT
{
    GRAPH <http://imn.htwk-leipzig.de/pbachman/ontologies/references#>
    {
        ?tunefindArtist owl:sameAs ?lastfmArtist .
        ?lastfmArtist owl:sameAs ?tunefindArtist .
    }
}
USING <http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#>
USING <http://imn.htwk-leipzig.de/pbachman/ontologies/lastfm#>
WHERE
{
    ?tunefindArtist a dbpedia-owl:MusicalArtist ;
                    dbpprop:name ?tunefindArtistName .

    FILTER(STRSTARTS(STR(?tunefindArtist), "http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#"))

    ?lastfmArtist a dbpedia-owl:MusicalArtist ;
                  dbpprop:name ?lastfmArtistName .

    FILTER(STRSTARTS(STR(?lastfmArtist), "http://imn.htwk-leipzig.de/pbachman/ontologies/lastfm#"))


    FILTER(lcase(STR(?tunefindArtistName)) = lcase(STR(?lastfmArtistName)))
}